! MACRO:         wp_for_crack_grad.mac
!  
! Date:          November 4, 2022
!
! Revision:      03
!
! Prepared By:   Lorenzo Cappelli
!				 
! 
! Disclaimer:    This macro is provided without guarantee, written or implied,
!                as to the validity of the information that it generates.
!                It is the responsibility of the user to independently verify
!                all results.  This macro may be freely distributed as long 
!                this header remains unchanged.
!
! Description:   This macro will plot S1 stresses on a working plane defined by direction cosines of S1

LS_num=ARG1 !Load step to extract orientation
n_plot=ARG2 ! Number of nodes to be evaluated
*del,snode
*del,stnode
*del,prindir
*del,kloc
*del,locnode
*del,tlocnode
*del,kloc_S1
*del,kloc_S2
*del,stnode_all_res
*del,prindir_all_res

*dim,snode_all_res,array,6,n_plot
*dim,stnode_all_res,array,n_plot,6
*dim,prindir_all_res,array,n_plot,9

*dim,snode,,6,1 
*dim,stnode,,1,6 
*dim,prindir,,1,9 
*dim,kloc_S1,,1,3
*dim,kloc_S2,,1,3
*dim,locnode,,3,1
*dim,tlocnode,,1,3

/post1
Inres,ALL   
FILE,'file','rst','.'   
set,LS_num,last	 
allsel,all
csys,0
rsys,0

*do,i,1,n_plot
! *VGET, ParR   , Entity, ENTNUM, Item1, IT1NUM, Item2, IT2NUM, KLOOP
*vget,snode(1,1),node   ,n_id_nostress(i ,1)  ,s     ,x,,,4   ! get the stress matrix by iteration on IT1NUM
*mfun,stnode(1,1),tran,snode(1,1) 				! transpose the array
*vfun,prindir(1,1),dircos,stnode(1,1) 			! directional cosines of principal stresses
*do,j,1,6
	snode_all_res(j,i)=snode(j,1)
	stnode_all_res(i,j)=stnode(1,j)
*enddo
*do,j,1,9
	prindir_all_res(i,j)=prindir(1,j)
*enddo
*enddo
/PREP7
*dim,k_all,array,n_plot,3
*do,i,1,n_plot
	knode,,n_id_nostress(i ,1)
*get,k_all(i,1),kp,0,num,max

! Node location XYZ
*vget,locnode(1,1),node,n_id_nostress(i ,1),loc,x,,,4
! Transpose the array
*mfun,tlocnode(1,1),tran,locnode(1,1) 

! S1 second point location
	kloc_S1(1,1)=tlocnode(1,1)+prindir_all_res(i,1)
	kloc_S1(1,2)=tlocnode(1,2)+prindir_all_res(i,2)
	kloc_S1(1,3)=tlocnode(1,3)+prindir_all_res(i,3)
	k,,kloc_S1(1,1),kloc_S1(1,2),kloc_S1(1,3)
*get,k_all(i,2),kp,0,num,max

! S2 second point location
	kloc_S2(1,1)=tlocnode(1,1)+prindir_all_res(i,4)
	kloc_S2(1,2)=tlocnode(1,2)+prindir_all_res(i,5)
	kloc_S2(1,3)=tlocnode(1,3)+prindir_all_res(i,6)
	k,,kloc_S2(1,1),kloc_S2(1,2),kloc_S2(1,3)
*get,k_all(i,3),kp,0,num,max
*enddo

finish
/post1
Inres,ALL   
FILE,'file','rst','.'   
set,LS_num,last	 
allsel,all
csys,0
rsys,0